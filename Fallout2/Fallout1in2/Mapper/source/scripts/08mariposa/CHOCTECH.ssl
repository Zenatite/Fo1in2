/*

   Military Base - CoC technicians, VATS location.

   NOTE:
      This script has dialog for the Cathedral, but the critters are never used there.

*/

/* Include Files */
#include "define.h"

#define NAME                    SCRIPT_CHOCTECH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_MBASE)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure look_at_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure combat;

procedure ChocTech00;
procedure ChocTech01;
procedure ChocTech02;
procedure ChocTech02a;
procedure ChocTech03;
procedure ChocTech04;
procedure ChocTech05;
procedure ChocTech06;
procedure ChocTech06a;
procedure ChocTech07;
procedure ChocTech08;
procedure ChocTech09;
procedure ChocTech10;
procedure ChocTech11;
procedure ChocTechend;

import variable Team9_Count;
import variable removal_ptr;

variable tmp_hostile;
variable said_stuff;
variable explode;
variable initial := 0;

procedure start begin
   if not(initial) then begin
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      set_self_ai(AI_CHILDREN_TRUE);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if (explode) then begin
         critter_dmg(self_obj, random(200, 250), DMG_explosion);
      end
      else begin
         if (self_can_see_dude) then begin
            if (dude_is_armed) then begin
               if (not(said_stuff)) then begin
                  said_stuff := 1;
                  // Team9_Count is never > 0, so disabled the check
                  //if (Team9_Count > 0) then begin
                  //   call ChocTech00;
                  //end
                  //else begin
                     call ChocTech01;
                  //end
               end
            end
         end
         else begin
            said_stuff := 0;
         end
      end
   end
end

procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

procedure talk_p_proc begin
   if (global_var(GVAR_MASTER_DEAD) == 1) then begin
      start_dialog_at_node(ChocTech10);
   end
   else if (global_var(GVAR_LIEUTENANTS_DEAD) == 1) then begin
      call ChocTech09;
   end
   else begin
      /*
         Team9 doesn't exist. Was thinking about changing it to Team8 in the same map, but
         in doesn't make sense in the end. So better to just keep it disabled.
         VSGTEAM9 and VATTEAM9 were the original guard scripts.
      */
      //if (Team9_Count > 0) then begin
         start_dialog_at_node(ChocTech02);
      //end
      //else begin
      //   call ChocTech01;
      //end
   end
end

procedure destroy_p_proc begin
   inc_inv_evil_crit
   set_global_var(GVAR_VATS_ALERT, 1);
end

procedure combat begin
   BottomReact
   tmp_hostile := 1;
end

procedure ChocTech00 begin
   float_msg(self_obj, mstr(random(101, 104)), FLOAT_MSG_NORMAL);
end

procedure ChocTech01 begin
   float_msg(self_obj, mstr(random(105, 107)), FLOAT_MSG_NORMAL);
end

procedure ChocTech02 begin
   Reply(108);
   NLowOption(109, ChocTech03);
   NOption(110, ChocTech04, 4);
   BOption(111, combat, 4);
   NOption(112, ChocTech02a, 6);
end

procedure ChocTech02a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call ChocTech06;
   end
   else begin
      call ChocTech05;
   end
end

procedure ChocTech03 begin
   NMessage(113);
end

procedure ChocTech04 begin
   NMessage(114);
end

procedure ChocTech05 begin
   NMessage(115);
   call combat;
end

procedure ChocTech06 begin
   Reply(116);
   BOption(117, combat, 5);
   NOption(118, ChocTech06a, 6);
end

procedure ChocTech06a begin
   if (dude_skill_success(SKILL_SPEECH, 0)) then begin
      call ChocTech07;
   end
   else begin
      call ChocTech05;
   end
end

procedure ChocTech07 begin
   Reply(119);
   NOption(120, ChocTechend, 6);
   NOption(121, ChocTech08, 6);
   NOption(122, ChocTech05, 6);
end

procedure ChocTech08 begin
   Reply(123);
   NOption(124, ChocTech05, 6);
end

procedure ChocTech09 begin
   float_msg(self_obj, mstr(125), FLOAT_MSG_NORMAL);
   explode := 1;
end

procedure ChocTech10 begin
   Reply(126);
   NLowOption(127, ChocTech11);
   NOption(128, ChocTech11, 4);
   BOption(129, combat, 4);
end

procedure ChocTech11 begin
   NMessage(130);
   explode := 1;
end

procedure ChocTechend begin
end
