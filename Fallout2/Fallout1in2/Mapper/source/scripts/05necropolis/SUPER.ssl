/*

   Necropolis - cut content, watershed mutant?

*/

/* Include Files */
#include "define.h"
#include "area/necropolis.h"

#define NAME                    SCRIPT_SUPER
#define TOWN_REP_VAR            (GVAR_TOWN_REP_NECROPOLIS)

#include "command.h"
#include "modreact.h"

/* Standard Script Procedures */
procedure start;
procedure talk_p_proc;
procedure pickup_p_proc;
procedure critter_p_proc;
procedure destroy_p_proc;
procedure description_p_proc;

procedure supercbt;
procedure superx;
procedure super00;

variable tmp_hostile;
variable init_teams := 0;

procedure start begin
   if (not(init_teams)) then begin
      set_self_team(TEAM_NECROPOLIS_MUTANT);
      init_teams := 1;
   end
end

procedure pickup_p_proc begin
   tmp_hostile := 1;
end

procedure critter_p_proc begin
   if (tmp_hostile) then begin
      tmp_hostile := 0;
      attack(dude_obj);
   end
   else begin
      if not(necropolis_invaded) then begin
         if not(is_loading_game) then set_self_invisible;
      end
      else begin
         if ((game_time_hour >= 1900) or (game_time_hour < 600) and (self_tile != 24929)) then begin
            self_walk_to_tile(24929);
         end
         if (day and (self_tile != 25915)) then begin
            self_walk_to_tile(25915);
         end
      end
   end
end

procedure destroy_p_proc begin
   set_global_var(GVAR_SUPER_MUTANTS_KILLED, global_var(GVAR_SUPER_MUTANTS_KILLED) + 1);
   inc_evil_critter
end

procedure description_p_proc begin
   display_msg(message_str(SCRIPT_SUPER, 100));
end

procedure talk_p_proc begin
   dude_look_at_self;
   start_gdialog(100, self_obj, 4, -1, -1);
   gsay_start;
   if (local_var(0)) then begin
      call super00;
   end
   else begin
      set_local_var(0, 1);
      call super00;
   end
   gsay_end;
   end_dialogue;
end

procedure supercbt begin
   tmp_hostile := 1;
end

procedure superx begin
   call supercbt;
end

procedure super00 begin
   gsay_reply(NAME, 101);
   giq_option(3, NAME, 102, superx, NEUTRAL_REACTION);
   giq_option(3, NAME, 103, supercbt, NEUTRAL_REACTION);
   giq_option(-3, NAME, 104, supercbt, NEUTRAL_REACTION);
end
