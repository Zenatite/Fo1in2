/*

   Hub - Water Merchants guard who's caravan got attacked by the Deathclaw

*/

#include "define.h"

#define NAME                    SCRIPT_TRENT
//#define TOWN_REP_VAR            (GVAR_TOWN_REP_HUB)

#include "command.h"
#include "modreact.h"

procedure start;
procedure critter_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure pickup_p_proc;
procedure use_obj_on_p_proc;
procedure talk_p_proc;

procedure Trent00;
procedure Trent01;
procedure Trent02;
procedure Trent03;
procedure Trent04;
procedure Trent05;
procedure Trent06;
procedure Trent07;
procedure Trent08;
procedure Trent09;
procedure Trent10;
procedure Trent11;
procedure Trent12;
procedure Trent13;
procedure Trent14;
procedure Trent15;
procedure Trent16;
procedure Trent17;
procedure Trent18;
procedure Trent19;
procedure Trent20;
procedure Trent21;
procedure Trent22;
procedure Trent23;
procedure Trent24;

procedure give_money;

variable calm;
variable initial := 0;
variable known;
variable scared;
variable close2dude;

variable direction;
variable prev_dist;

#define set_trent_herebefore  set_global_var(GVAR_TRENT_STATUS, global_var(GVAR_TRENT_STATUS) bwor 1)
#define set_trent_killed      set_global_var(GVAR_TRENT_STATUS, global_var(GVAR_TRENT_STATUS) bwor 8)

#define already_met_once      ((global_var(GVAR_TRENT_STATUS) bwand 1) != 0)

procedure start begin
   if (not(initial) and map_first_run) then begin
      set_self_team(TEAM_HUB_WATER_MERCHANTS);
      set_self_ai(AI_CAREFUL);
      initial := 1;
   end
end

procedure critter_p_proc begin
   if (scared and (tile_distance_objs(self_obj, dude_obj) < 60)) then begin
      call flee_dude;
   end
   else begin
      if (not(close2dude)) then begin
         if (tile_distance_objs(dude_obj, self_obj) <= 2) then begin
            close2dude := 1;
         end
         else begin
            direction := rotation_to_tile(dude_tile % 200, dude_tile / 200);
            dest_tile := tile_num_in_direction(dude_tile, direction, 1);
            self_run_to_tile(dest_tile);
            if ((random(1, 18) == 1) and not(calm)) then begin
               float_msg(self_obj, mstr(141), FLOAT_MSG_NORMAL);
            end
         end
      end
      else begin
         if (not(scared)) then begin
            if (random(1, 4) == 1) then begin
               self_look_at_dude;
            end
         end
      end
   end
end

procedure damage_p_proc begin
   if source_is_dude then begin
      scared := 1;
   end
   script_overrides;
   call flee_dude;
end

procedure destroy_p_proc begin
   inc_good_critter
   set_trent_killed;
end

procedure pickup_p_proc begin
   scared := 1;
end

procedure use_obj_on_p_proc begin
   variable LVar0 := 0;
   if ((obj_pid(obj_being_used_with) == PID_IGUANA_ON_A_STICK) or (obj_pid(obj_being_used_with) == PID_MEAT_ON_A_STICK) or (obj_pid(obj_being_used_with) == PID_WATER_FLASK) or (obj_pid(obj_being_used_with) == PID_MEAT_JERKY)) then begin
      LVar0 := obj_being_used_with;
      rm_obj_from_inven(source_obj, obj_being_used_with);
      destroy_object(LVar0);
      if (self_caps == 100) then begin
         if (scared) then begin
            call Trent21;
         end
         else begin
            call Trent22;
         end
      end
      else begin
         if (scared) then begin
            call Trent23;
         end
         else begin
            call Trent24;
         end
      end
      calm := 1;
      scared := 0;
   end
end

procedure talk_p_proc begin
   dude_look_at_self;
   if ((scared == 1) or (check_general_rep < -20)) then begin
      call Trent00;
   end
   else begin
      start_gdialog(703, self_obj, 4, -1, -1);
      gsay_start;
      get_reaction
      if (calm) then begin
         call Trent16;
      end
      else begin
         if already_met_once then begin
            call Trent02;
         end
         else begin
            call Trent01;
         end
      end
      gsay_end;
      end_dialogue;
   end
end

procedure Trent00 begin
   float_msg(self_obj, mstr(100), FLOAT_MSG_NORMAL);
end

procedure Trent01 begin
   set_trent_herebefore;
   Reply(101);
   if (global_var(GVAR_MISSING_CARAVAN) == 1) then begin
      NOption(102, Trent03, 7);
   end
   NOption(103, Trent04, 4);
   NOption(104, Trent03, 4);
   BOption(105, Trent05, 4);
   if looking_for_waterchip then begin
      NOption(106, Trent06, 4);
   end
   if (global_var(GVAR_KILL_DEATHCLAW) == 1) then begin
      NOption(107, Trent07, 4);
   end
   BLowOption(108, Trent08);
end

procedure Trent02 begin
   Reply(109);
   NOption(103, Trent04, 4);
   NOption(104, Trent03, 4);
end

procedure Trent03 begin
   Reply(111);
   NOption(112, Trent11, 4);
   NOption(113, Trent12, 4);
   NOption(114, Trent11, 4);
   NOption(115, Trent13, 4);
end

procedure Trent04 begin
   Reply(116);
   NOption(117, Trent11, 4);
   NOption(118, Trent14, 4);
   if (global_var(GVAR_KILL_DEATHCLAW) == 1) then begin
      NOption(119, Trent07, 4);
   end
   NOption(120, Trent15, 4);
end

procedure Trent05 begin
   variable LVar0 := 0;
   variable LVar1 := 0;
   calm := 0;
   scared := 1;
   BMessage(121);
   if (self_item_count(PID_DESERT_EAGLE)) then begin
      LVar0 := self_item(PID_DESERT_EAGLE);
      rm_obj_from_inven(self_obj, LVar0);
      add_obj_to_inven(dude_obj, LVar0);
   end
   if (self_item_count(PID_44_MAGNUM_JHP)) then begin
      LVar0 := self_item(PID_44_MAGNUM_JHP);
      rm_obj_from_inven(self_obj, LVar0);
      add_obj_to_inven(dude_obj, LVar0);
   end
   if (self_item_count(PID_44_FMJ_MAGNUM)) then begin
      LVar0 := self_item(PID_44_FMJ_MAGNUM);
      rm_obj_from_inven(self_obj, LVar0);
      add_obj_to_inven(dude_obj, LVar0);
   end
   if (self_item_count(PID_KNIFE)) then begin
      LVar0 := self_item(PID_KNIFE);
      rm_obj_from_inven(self_obj, LVar0);
      add_obj_to_inven(dude_obj, LVar0);
   end
end

procedure Trent06 begin
   NMessage(122);
end

procedure Trent07 begin
   scared := 1;
   NMessage(123);
end

procedure Trent08 begin
   BMessage(124);
end

procedure Trent09 begin
   NMessage(125);
end

procedure Trent10 begin
   if (dude_is_male) then begin
      NMessage(142);
   end
   else begin
      NMessage(143);
   end
end

procedure Trent11 begin
   NMessage(127);
end

procedure Trent12 begin
   NMessage(128);
end

procedure Trent13 begin
   NMessage(129);
end

procedure Trent14 begin
   NMessage(130);
end

procedure Trent15 begin
   NMessage(131);
end

procedure Trent16 begin
   Reply(132);
   if (global_var(GVAR_MISSING_CARAVAN) == 1) then begin
      NOption(102, Trent17, 7);
   end
   NOption(103, Trent18, 4);
   BOption(105, Trent05, 4);
   if looking_for_waterchip then begin
      NOption(106, Trent19, 4);
   end
   if (global_var(GVAR_KILL_DEATHCLAW) == 1) then begin
      NOption(107, Trent20, 4);
   end
   NLowOption(108, Trent10);
end

procedure Trent17 begin
   NMessage(133);
end

procedure Trent18 begin
   NMessage(134);
end

procedure Trent19 begin
   NMessage(135);
end

procedure Trent20 begin
   NMessage(136);
end

procedure Trent21 begin
   float_msg(self_obj, mstr(137), FLOAT_MSG_GREEN);
   call give_money;
end

procedure Trent22 begin
   float_msg(self_obj, mstr(138), FLOAT_MSG_GREEN);
   call give_money;
end

procedure Trent23 begin
   float_msg(self_obj, mstr(138), FLOAT_MSG_GREEN);
   call give_money;
end

procedure Trent24 begin
   float_msg(self_obj, mstr(139), FLOAT_MSG_GREEN);
   call give_money;
end

procedure give_money begin
   variable LVar0 := 0;
   LVar0 := self_caps;
   if (LVar0) then begin
      self_caps_adjust(-1 * LVar0);
      dude_caps_adjust(LVar0);
   end
end
